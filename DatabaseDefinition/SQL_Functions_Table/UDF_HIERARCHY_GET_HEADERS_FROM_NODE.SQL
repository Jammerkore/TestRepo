--dv =============================================
--dv Create date:	10/23/2014
--dv Description:	Retrieves headers for organizational and alternate hierarchies down to color level
--dv History:		12/10/2014 TT#1419-MD -jsobek -Header Filter using an Alternate Hierarchy returns no results in the Allocation Workspace
--dv				02/13/2015 TT#4306 -jsobek -Filter on Alternate Hierarchy Performance
--dv =============================================
CREATE FUNCTION [dbo].[UDF_HIERARCHY_GET_HEADERS_FROM_NODE] 
(	
	@SELECTED_NODE_RID INT,
	@SELECTED_PH_RID INT,
	@ORGANIZATIONAL_PH_RID INT
)
RETURNS TABLE 
AS
RETURN 
(
	WITH cte_descendants(HN_RID, HN_TYPE, HOME_PH_RID, HOME_LEVEL, ACTIVE_IND, PH_RID, PARENT_HN_RID, PARENT_PH_RID)
		AS (
			SELECT
				hn.HN_RID, 
				hn.HN_TYPE, 
				hn.HOME_PH_RID,
				hn.HOME_LEVEL, 
				hn.ACTIVE_IND, 
				hnj.PH_RID, 
				hnj.PARENT_HN_RID,
				hn.HOME_PH_RID as PARENT_PH_RID
			FROM HIER_NODE_JOIN hnj WITH (NOLOCK)
			INNER JOIN HIERARCHY_NODE hn WITH (NOLOCK) ON hn.HN_RID = hnj.HN_RID
			WHERE hn.HN_RID=@SELECTED_NODE_RID AND PH_RID=@SELECTED_PH_RID
		
			UNION ALL
			SELECT 
				hn.HN_RID, 
				hn.HN_TYPE, 
				hn.HOME_PH_RID,
				hn.HOME_LEVEL, 
				hn.ACTIVE_IND, 
				hnj.PH_RID, 
				hnj.PARENT_HN_RID,
				cte.HOME_PH_RID AS PARENT_PH_RID
			FROM HIER_NODE_JOIN hnj WITH (NOLOCK)
			INNER JOIN HIERARCHY_NODE hn WITH (NOLOCK) ON hn.HN_RID = hnj.HN_RID AND hn.HN_TYPE != 800204  --Exclude all sizes
			--AND (( hn.HN_TYPE != 800203 AND  @SELECTED_PH_RID = @ORGANIZATIONAL_PH_RID) OR @SELECTED_PH_RID != @ORGANIZATIONAL_PH_RID )  
			-- AND coalesce(hn.ACTIVE_IND, 1)=1
			INNER JOIN cte_descendants cte ON hnj.PARENT_HN_RID = cte.HN_RID 
			WHERE cte.HN_TYPE != 800202 AND cte.HN_TYPE != 800203
			)

  
	SELECT 
		h.HDR_RID--, 'Style Join' as JOIN_TYPE
	FROM
	(
		SELECT 
			HN_RID, 
			ACTIVE_IND,

		CASE WHEN cte.HN_TYPE = 800203 THEN
			(SELECT cn.COLOR_CODE_RID FROM COLOR_NODE cn WITH (NOLOCK) WHERE  cn.HN_RID = cte.HN_RID)
		ELSE
			 NULL
		END AS COLOR_CODE_RID,
	
		CASE WHEN cte.HN_TYPE = 800203 THEN
			(SELECT hnj.PARENT_HN_RID FROM HIER_NODE_JOIN hnj WITH (NOLOCK) WHERE hnj.PH_RID=@ORGANIZATIONAL_PH_RID AND hnj.HN_RID = cte.HN_RID)
		ELSE
			 NULL
		END AS COLOR_STYLE_HNRID

		FROM	cte_descendants cte 
		--WHERE PARENT_PH_RID = @SELECTED_PH_RID AND HOME_PH_RID=@ORGANIZATIONAL_PH_RID
	) AS q1
	INNER JOIN HEADER h WITH (NOLOCK)  ON h.STYLE_HNRID = q1.HN_RID  AND q1.COLOR_CODE_RID IS NULL

	UNION

	SELECT 
		h2.HDR_RID--, 'Bulk Color Join' as JOIN_TYPE
	FROM
	(
		SELECT 
			HN_RID, 
			ACTIVE_IND,

		CASE WHEN cte.HN_TYPE = 800203 THEN
			(SELECT cn.COLOR_CODE_RID FROM COLOR_NODE cn WITH (NOLOCK) WHERE  cn.HN_RID = cte.HN_RID)
		ELSE
			 NULL
		END AS COLOR_CODE_RID,
	
		CASE WHEN cte.HN_TYPE = 800203 THEN
			(SELECT hnj.PARENT_HN_RID FROM HIER_NODE_JOIN hnj WITH (NOLOCK) WHERE hnj.PH_RID=@ORGANIZATIONAL_PH_RID AND hnj.HN_RID = cte.HN_RID)
		ELSE
			 NULL
		END AS COLOR_STYLE_HNRID

		FROM	cte_descendants cte 
		--WHERE PARENT_PH_RID = @SELECTED_PH_RID AND HOME_PH_RID=@ORGANIZATIONAL_PH_RID
	) AS q2
	INNER JOIN HEADER h2 WITH (NOLOCK)  ON h2.STYLE_HNRID = q2.COLOR_STYLE_HNRID   AND q2.COLOR_CODE_RID IS NOT NULL
	INNER JOIN HEADER_BULK_COLOR hbc WITH (NOLOCK) ON hbc.HDR_RID = h2.HDR_RID AND hbc.COLOR_CODE_RID = q2. COLOR_CODE_RID

	UNION

	SELECT 
		h3.HDR_RID--, 'Pack Join' as JOIN_TYPE
	FROM
	(
		SELECT 
			HN_RID, 
			ACTIVE_IND,

			CASE WHEN cte.HN_TYPE = 800203 THEN
			(SELECT cn.COLOR_CODE_RID FROM COLOR_NODE cn WITH (NOLOCK) WHERE  cn.HN_RID = cte.HN_RID)
		ELSE
			 NULL
		END AS COLOR_CODE_RID,
	
		CASE WHEN cte.HN_TYPE = 800203 THEN
			(SELECT hnj.PARENT_HN_RID FROM HIER_NODE_JOIN hnj WITH (NOLOCK) WHERE hnj.PH_RID=@ORGANIZATIONAL_PH_RID AND hnj.HN_RID = cte.HN_RID)
		ELSE
			 NULL
		END AS COLOR_STYLE_HNRID

		FROM	cte_descendants cte 
		--WHERE PARENT_PH_RID = @SELECTED_PH_RID AND HOME_PH_RID=@ORGANIZATIONAL_PH_RID
	) AS q3
	INNER JOIN HEADER h3 WITH (NOLOCK)  ON h3.STYLE_HNRID=q3.COLOR_STYLE_HNRID  AND q3.COLOR_CODE_RID IS NOT NULL
	INNER JOIN HEADER_PACK hp WITH (NOLOCK) ON hp.HDR_RID = h3.HDR_RID 
	INNER JOIN HEADER_PACK_COLOR hpc WITH (NOLOCK) ON hpc.HDR_PACK_RID = hp.HDR_PACK_RID AND hpc.COLOR_CODE_RID = q3.COLOR_CODE_RID 

)
GO