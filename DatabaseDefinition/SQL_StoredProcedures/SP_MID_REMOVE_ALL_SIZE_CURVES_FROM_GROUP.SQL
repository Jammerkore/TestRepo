CREATE PROCEDURE [dbo].[SP_MID_REMOVE_ALL_SIZE_CURVES_FROM_GROUP] 
	(@inSizeCurveGroup VARCHAR(250))
AS
BEGIN
	-- This Store Procedure is called during the Size Curve Group Write process from the Size Curve Method to remove all
	-- Size Curves from the Size Curve Group.  This procedure can also be executed externally by the Client to clear a Size Curve Group.
	-- PLEASE NOTE: IF THIS PROCEDURE IS EXECUTED EXTERNALLY, THE SIZE CURVE GROUP WILL NOT BE VALID UNTIL THE GROUP IS RELOADED DUE
	-- TO THE REMOVAL OF THE DEFAULT SIZE CURVE.
	DECLARE @SzCrvGrpRID INT

	SET NOCOUNT ON;
	-- Get the RID of the Curve Group
	SELECT @SzCrvGrpRID = SIZE_CURVE_GROUP_RID
	FROM SIZE_CURVE_GROUP
	WHERE SIZE_CURVE_GROUP_NAME = @inSizeCurveGroup

	-- Create a temp table of all Curves used by this Curve Group
	SELECT SIZE_CURVE_RID
	INTO #TEMP1
	FROM SIZE_CURVE_GROUP_JOIN
	WHERE SIZE_CURVE_GROUP_RID = @SzCrvGrpRID

	INSERT INTO #TEMP1 (SIZE_CURVE_RID)
	SELECT DEFAULT_SIZE_CURVE_RID
	FROM SIZE_CURVE_GROUP
	WHERE SIZE_CURVE_GROUP_RID = @SzCrvGrpRID
		AND DEFAULT_SIZE_CURVE_RID IS NOT NULL

	-- Delete all references to the Curves from the Curve Group
	INSERT INTO VIRTUAL_LOCK (
		LOCK_TYPE,
		LOCK_ID
		)
	VALUES (
		30,
		'SIZE_CURVE_GENERATE_LOCK'
		)

	UPDATE SIZE_CURVE_GROUP
	WITH (ROWLOCK)

	SET DEFAULT_SIZE_CURVE_RID = NULL
	WHERE SIZE_CURVE_GROUP_RID = @SzCrvGrpRID

	DELETE
	FROM SIZE_CURVE_GROUP_JOIN WITH (ROWLOCK)
	WHERE SIZE_CURVE_GROUP_RID = @SzCrvGrpRID

	DELETE
	FROM STORE_SIZE_CURVE_BY_GROUP WITH (ROWLOCK)
	WHERE SIZE_CURVE_GROUP_RID = @SzCrvGrpRID

	-- Delete all unused Curves
	DELETE
	FROM SIZE_CURVE_JOIN WITH (ROWLOCK)
	WHERE SIZE_CURVE_RID IN (
			SELECT SIZE_CURVE_RID
			FROM #TEMP1
			)

	DELETE
	FROM SIZE_CURVE WITH (ROWLOCK)
	WHERE SIZE_CURVE_RID IN (
			SELECT SIZE_CURVE_RID
			FROM #TEMP1
			)

	DELETE
	FROM VIRTUAL_LOCK
	WHERE LOCK_TYPE = 30
		AND LOCK_ID = 'SIZE_CURVE_GENERATE_LOCK'

	DROP TABLE #TEMP1
	SELECT @@ROWCOUNT;
END
GO


