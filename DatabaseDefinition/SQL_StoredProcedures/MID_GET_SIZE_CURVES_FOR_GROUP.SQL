--dv Create date:	9/18/2013
--dv Description:	Returns the group name and the default curve for the group in the first table
--dv				Returns size curve data in the second table
--dv History:		10/7/2014 TT#1327-MD -jsobek -Error with MID_GET_SIZE_CURVES_FOR_GROUP
--dv =============================================
CREATE PROCEDURE [dbo].[MID_GET_SIZE_CURVES_FOR_GROUP]
	@SizeCurveGroupRid int
AS
BEGIN
	SET NOCOUNT ON;

	--Returns the group name and the default curve for the group in the first table
	SELECT 
		SCG.SIZE_CURVE_GROUP_RID, 
		SIZE_CURVE_GROUP_NAME, 
		DEFAULT_SIZE_CURVE_RID, 
		DEFINED_SIZE_GROUP_RID,
		SIZE_CURVE_RID  
	FROM SIZE_CURVE_GROUP SCG 
	LEFT OUTER JOIN SIZE_CURVE_GROUP_JOIN SCGJ ON SCG.SIZE_CURVE_GROUP_RID = SCGJ.SIZE_CURVE_GROUP_RID 
	WHERE SCG.SIZE_CURVE_GROUP_RID = @SizeCurveGroupRid



	--Returns size curve data in the second table
	DECLARE @NoSizeDimensionLbl varchar(500)
	SET @NoSizeDimensionLbl = (SELECT TEXT_VALUE FROM APPLICATION_TEXT WHERE TEXT_CODE=900356);

	SELECT 
		SC.SIZE_CURVE_RID, 
		SIZE_CURVE_NAME, 
		CD.SIZE_CODE_RID, 
		0 SEQ, 
		CD.SIZE_CODE_ID,
		COALESCE([PERCENT],0) [PERCENT], 
		CD.SIZE_CODE_PRIMARY, 
		COALESCE(CD.SIZE_CODE_SECONDARY, @NoSizeDimensionLbl) as SIZE_CODE_SECONDARY, 
		CD.SIZE_CODE_PRODUCT_CATEGORY, 
		SZ.SIZES_RID, 
		COALESCE(DIM.DIMENSIONS_RID, -1) DIMENSIONS_RID 
	FROM SIZE_CURVE SC 
	INNER JOIN SIZE_CURVE_JOIN SCJ ON SC.SIZE_CURVE_RID = SCJ.SIZE_CURVE_RID 
	INNER JOIN SIZE_CODE CD ON SCJ.SIZE_CODE_RID = CD.SIZE_CODE_RID 
	INNER JOIN SIZES SZ ON SZ.SIZE_CODE_PRIMARY = CD.SIZE_CODE_PRIMARY 
	LEFT OUTER JOIN DIMENSIONS DIM ON CD.SIZE_CODE_SECONDARY = DIM.SIZE_CODE_SECONDARY 
	WHERE SC.SIZE_CURVE_RID IN (
		SELECT SCGJ.SIZE_CURVE_RID  
		FROM SIZE_CURVE_GROUP SCG 
		INNER JOIN SIZE_CURVE_GROUP_JOIN SCGJ ON SCG.SIZE_CURVE_GROUP_RID = SCGJ.SIZE_CURVE_GROUP_RID 
		WHERE SCG.SIZE_CURVE_GROUP_RID = @SizeCurveGroupRid
	)
	ORDER BY CD.SIZE_CODE_PRIMARY, CD.SIZE_CODE_SECONDARY
END
GO
