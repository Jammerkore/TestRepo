--dv =============================================
--dv Create date: 2/23/2015
--dv Description:	Get all fields and selected characteristics for all stores.
--dv =============================================
CREATE PROCEDURE [dbo].[MID_STORES_READ_FIELDS_FOR_MAINT_BY_COL]
	@INCLUDE_INACTIVE_STORES bit,
	@CHAR_GROUP_RID_LIST CHAR_GROUP_RID_TYPE READONLY
AS
BEGIN
	SET NOCOUNT ON;

	--Getting all field values regardless of field selection
	SELECT 
		s.ST_RID,
		[dbo].[UDF_MID_GET_STORE_TEXT](s.ST_RID) STORE_TEXT,
		COALESCE(s.ST_ID, '') ST_ID,
		COALESCE(STORE_NAME, '') STORE_NAME,
		COALESCE(STORE_DESC, '') STORE_DESC,
		ACTIVE_IND,
		COALESCE(CITY, '') CITY,
		COALESCE(STATE, '') STATE,
		COALESCE(SELLING_SQ_FT, 0) SELLING_SQ_FT,
		SELLING_OPEN_DATE,
		SELLING_CLOSE_DATE,
		STOCK_OPEN_DATE,
		STOCK_CLOSE_DATE,
		COALESCE(LEAD_TIME, 0) LEAD_TIME,
		SHIP_ON_MONDAY,
		SHIP_ON_TUESDAY,
		SHIP_ON_WEDNESDAY,
		SHIP_ON_THURSDAY,
		SHIP_ON_FRIDAY,
		SHIP_ON_SATURDAY,
		SHIP_ON_SUNDAY,
		SIMILAR_STORE_MODEL,
		IMO_ID,
		STORE_DELETE_IND
	FROM STORES s
	WHERE (@INCLUDE_INACTIVE_STORES = 1 OR s.ACTIVE_IND='1')
	ORDER BY s.ST_ID

	SELECT 
		scj.ST_RID,
		scg.SCG_RID,
		COALESCE(
		CASE 		
			WHEN scg.SCG_TYPE=1 THEN CONVERT(varchar(50), sc.DATE_VALUE, 101)
			WHEN scg.SCG_TYPE=2 THEN CONVERT(varchar(50), sc.NUMBER_VALUE)
			WHEN scg.SCG_TYPE=3 THEN CONVERT(varchar(50), sc.DOLLAR_VALUE)
			ELSE sc.TEXT_VALUE
		END, '')  AS CHAR_VALUE
	FROM STORE_CHAR_GROUP scg
	INNER JOIN STORE_CHAR sc ON sc.SCG_RID = scg.SCG_RID 
	INNER JOIN STORE_CHAR_JOIN scj ON sc.SC_RID = scj.SC_RID
	WHERE scg.SCG_RID IN (SELECT CHAR_GROUP_RID FROM @CHAR_GROUP_RID_LIST)
	

	SELECT 
		scg.SCG_RID,
		scg.SCG_ID,
		scg.SCG_LIST_IND,
		scg.SCG_TYPE	
	FROM STORE_CHAR_GROUP scg
	WHERE scg.SCG_RID IN (SELECT CHAR_GROUP_RID FROM @CHAR_GROUP_RID_LIST)
	ORDER BY scg.SCG_ID


END
GO