--dv =============================================
--dv Create date: 
--dv Description: Set the batch number for the ROLLUP_ITEM
--dv Modified date:	3/2/2017
--dv                TT#5485 - JSmith - Performance
--dv =============================================
CREATE PROCEDURE [dbo].[SP_MID_SET_ROLLUP_BATCHES] 
	(
	@PROCESS INT,
	@PH_RID INT,
	@HOME_LEVEL INT,
	@ITEM_TYPE INT,
	@FV_RID INT,
	@BATCH_SIZE INT,
	@TABLE_NUMBER INT,
	@BATCH_NUMBER INT,
	@debug BIT = 0,
	@BATCH_COUNT INT OUTPUT
	)
AS
-- initialize work variables
DECLARE @batchnumber INT
DECLARE @numrows INT

SET @batchnumber = @BATCH_NUMBER
SET @numrows = 1

DECLARE @TEMP_ROLLUP_ITEM TABLE ( 
	PROCESS INT,
	HN_RID INT,
	TIME_ID INT,
	FV_RID INT,
	ITEM_TYPE INT
	) 

DECLARE @TEMP_ROLLUP_ITEM2 TABLE ( 
	BN INT,
	PROCESS INT,
	HN_RID INT,
	TIME_ID INT,
	FV_RID INT,
	ITEM_TYPE INT
	primary key (PROCESS, HN_RID, TIME_ID, FV_RID, ITEM_TYPE)
	) 

-- get records to add batch number
INSERT INTO @TEMP_ROLLUP_ITEM
SELECT PROCESS, HN_RID, TIME_ID, FV_RID, ITEM_TYPE
FROM ROLLUP_ITEM ri WITH (NOLOCK)
WHERE PROCESS = @PROCESS
	AND FV_RID = @FV_RID
	AND ITEM_TYPE = @ITEM_TYPE
	AND HN_MOD = @TABLE_NUMBER
	AND HOME_LEVEL = @HOME_LEVEL
	AND PH_RID = @PH_RID
	AND BATCH_NUMBER IS NULL
ORDER BY PROCESS,
	HN_RID,
	TIME_ID,
	FV_RID,
	ITEM_TYPE

-- calculate batch number from row number
INSERT INTO @TEMP_ROLLUP_ITEM2
SELECT ROW_NUMBER() OVER (
		ORDER BY PROCESS,
			HN_RID,
			FV_RID,
			ITEM_TYPE
		) / (@BATCH_SIZE) + @BATCH_NUMBER + 1 AS 'BN',
	PROCESS, HN_RID, TIME_ID, FV_RID, ITEM_TYPE
FROM @TEMP_ROLLUP_ITEM ri
ORDER BY PROCESS,
	HN_RID,
	TIME_ID,
	FV_RID,
	ITEM_TYPE

---- update main table
UPDATE ROLLUP_ITEM
WITH (ROWLOCK)

SET BATCH_NUMBER = tri.BN
FROM ROLLUP_ITEM ri
JOIN @TEMP_ROLLUP_ITEM2 tri ON tri.PROCESS = ri.PROCESS
	AND tri.HN_RID = ri.HN_RID
	AND tri.TIME_ID = ri.TIME_ID
	AND tri.FV_RID = ri.FV_RID
	AND tri.ITEM_TYPE = ri.ITEM_TYPE

--select @BATCH_COUNT = MAX(BN) from #TEMP_ROLLUP_ITEM2
SELECT @BATCH_COUNT = MAX(BATCH_NUMBER)
FROM ROLLUP_ITEM ri WITH (NOLOCK)
WHERE PROCESS = @PROCESS
	AND FV_RID = @FV_RID
	AND ITEM_TYPE = @ITEM_TYPE
	AND HOME_LEVEL = @HOME_LEVEL
	AND PH_RID = @PH_RID

IF @BATCH_COUNT IS NULL
	SET @BATCH_COUNT = 0

GO


