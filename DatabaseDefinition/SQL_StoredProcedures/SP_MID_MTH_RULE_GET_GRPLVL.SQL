--dv =============================================
--dv Create date:	3/31/2004 - STEVE TODD 
--dv Description:	GETS DATA OUT OF SIZE_MTH_RULE_GRPLVL
--dv Usage:			EXEC SP_MID_MTH_RULE_GET_GRPLVL 305, 111   
--dv History:		5/13/2015 -TT#1517-MD -jsobek -Store Service Optimization
--dv =============================================
CREATE PROCEDURE [dbo].[SP_MID_MTH_RULE_GET_GRPLVL] 
	(
	@METHODRID INT,
	@SGRID INT,
	@SG_VERSION INT   -- TT#1892-MD - JSmith - Versioning Test - Select Size Need Method receive SystemArgumentException Error
	)
AS
BEGIN
	SET NOCOUNT ON
	SET ANSI_PADDING ON

	DECLARE @ALLSTORE_ROW INT,
		@SET_ROW INT,
		@ALLSTORE_SGL_RID INT,
		@DEFAULT_ROW_SGL_RID INT,
		@DEFAULT_ROW INT

	SELECT @ALLSTORE_ROW = 0,
		@SET_ROW = 1,
		@DEFAULT_ROW = 8,
		@DEFAULT_ROW_SGL_RID = - 1

	CREATE TABLE #ALL_SZ (
		BAND_DSC VARCHAR(50) NOT NULL,
		METHOD_RID INT NOT NULL,
		SGL_RID INT NOT NULL,
		SIZE_RULE VARCHAR(20) NULL,
		SIZE_QUANTITY INT NULL,
		ROW_TYPE_ID INT NOT NULL
		)

	CREATE INDEX #ALL_SZ_IDX_1 ON #ALL_SZ (ROW_TYPE_ID)

	CREATE INDEX #ALL_SZ_IDX_2 ON #ALL_SZ (SGL_RID)

	/*NEXT GET THE DEFAULT ROW*/
	INSERT INTO #ALL_SZ
	SELECT 'Default',
		MGL.METHOD_RID,
		MGL.SGL_RID,
		MGL.SIZE_RULE,
		MGL.SIZE_QUANTITY,
		MGL.ROW_TYPE_ID
	FROM SIZE_MTH_RULE_GRPLVL MGL
	WHERE METHOD_RID = @METHODRID
		AND MGL.ROW_TYPE_ID = @DEFAULT_ROW

	/*ADD DEFAULT ROW IF NO DEFAULT ROWS ARE IN THE TABLE*/
	IF NOT EXISTS (
			SELECT *
			FROM #ALL_SZ
			WHERE SGL_RID = @DEFAULT_ROW_SGL_RID
			)
	BEGIN
		INSERT INTO #ALL_SZ
		VALUES (
			'Default',
			@METHODRID, --METHOD_RID
			@DEFAULT_ROW_SGL_RID, --SGL_RID
			NULL, --SIZE_RULE
			NULL, --SIZE_QUANTITY
			@DEFAULT_ROW
			) --ROW INDICATOR
	END

	/*FINALLY GO GET THE STORE SET ATTRIBUTES*/
	INSERT INTO #ALL_SZ
	SELECT SGL.SGL_OVERRIDE_ID,
		MGL.METHOD_RID,
		MGL.SGL_RID,
		MGL.SIZE_RULE,
		MGL.SIZE_QUANTITY,
		MGL.ROW_TYPE_ID
	FROM SIZE_MTH_RULE_GRPLVL MGL,
		STORE_GROUP_JOIN SGL
	WHERE METHOD_RID = @METHODRID
		AND MGL.SGL_RID = SGL.SGL_RID
		AND MGL.ROW_TYPE_ID = @SET_ROW
		AND SGL.SG_VERSION = @SG_VERSION   -- TT#1892-MD - JSmith - Versioning Test - Select Size Need Method receive SystemArgumentException Error
	ORDER BY SGL.SGL_OVERRIDE_SEQUENCE

	/*INSERT ROWS FOUND IN STORE_GROUP_LEVEL THAT ARE NOT IN THE METHOD TABLES*/
	INSERT INTO #ALL_SZ
	SELECT SGL.SGL_OVERRIDE_ID, --SGL_ID
		@METHODRID, --METHOD_RID
		SGL.SGL_RID, --SGL_RID
		NULL, --SIZE_RULE
		NULL, --SIZE_QUANTITY
		@SET_ROW --ROW INDICATOR
	FROM STORE_GROUP_JOIN SGL
	WHERE SG_RID = @SGRID
	    AND SGL.SG_VERSION = @SG_VERSION   -- TT#1892-MD - JSmith - Versioning Test - Select Size Need Method receive SystemArgumentException Error
		AND SGL.SGL_RID NOT IN (
			SELECT SGL_RID
			FROM #ALL_SZ
			WHERE ROW_TYPE_ID = @SET_ROW
			)
	ORDER BY SGL.SGL_OVERRIDE_SEQUENCE

	SELECT *
	FROM #ALL_SZ

	SET NOCOUNT OFF
	SET ANSI_PADDING OFF
END
GO


